spriteset(spriteset_br_720_purchase, "gfx/Aventra.png") {template_purchase(400, 0)}

// Greater Anglia base and reversed sprites.
spriteset(spriteset_br_720_front_GA, "gfx/Aventra.png") { template_train32px_old(0, 0) }
spriteset(spriteset_br_720_middle_GA, "gfx/Aventra.png") { template_train32px_old(0, 25) }
spriteset(spriteset_br_720_middle_panto_GA, "gfx/Aventra.png") { template_train32px_old(0, 50) }
spriteset(spriteset_br_720_rear_GA, "gfx/Aventra.png") { template_train32px_old(0, 75) }
spriteset(spriteset_br_720_front_GA_reversed, "gfx/Aventra.png") { template_train32px_old_reversed(200, 0) }
spriteset(spriteset_br_720_middle_GA_reversed, "gfx/Aventra.png") { template_train32px_old_reversed(0, 25) }
spriteset(spriteset_br_720_middle_panto_GA_reversed, "gfx/Aventra.png") { template_train32px_old_reversed(0, 50) }
spriteset(spriteset_br_720_rear_GA_reversed, "gfx/Aventra.png") { template_train32px_old_reversed(200, 75) }

// C2C base and reversed sprites.
spriteset(spriteset_br_720_front_C2C, "gfx/Aventra.png") { template_train32px_old(0, 400) }
spriteset(spriteset_br_720_middle_C2C, "gfx/Aventra.png") { template_train32px_old(0, 425) }
spriteset(spriteset_br_720_middle_panto_C2C, "gfx/Aventra.png") { template_train32px_old(0, 450) }
spriteset(spriteset_br_720_rear_C2C, "gfx/Aventra.png") { template_train32px_old(0, 475) }
spriteset(spriteset_br_720_front_C2C_reversed, "gfx/Aventra.png") { template_train32px_old_reversed(200, 400) }
spriteset(spriteset_br_720_middle_C2C_reversed, "gfx/Aventra.png") { template_train32px_old_reversed(0, 425) }
spriteset(spriteset_br_720_middle_panto_C2C_reversed, "gfx/Aventra.png") { template_train32px_old_reversed(0, 450) }
spriteset(spriteset_br_720_rear_C2C_reversed, "gfx/Aventra.png") { template_train32px_old_reversed(200, 475) }

// Class 720 graphics + selector flow (single articulated item ID).
//
// Runtime call order is:
// Step 1: switch_br_720_spriteset
//   -> Step 2: switch_br_720_direction_GA / switch_br_720_direction_C2C
//      -> Step 3: forward role selector (if not reversed), or
//      -> Step 4: mirrored-livery selector -> Step 5: mirrored-role selector (if reversed).
//
// Note: definitions below are grouped by helper function, not listed in runtime order.

// Step 3 (forward path): pick role from articulated index in each 5-car set.
// position_in_articulated_veh cycles 0..4 within a Class 720 unit.
// End of forward path 
switch(FEAT_TRAINS, SELF, switch_br_720_spriteset_GA_fwd, position_in_articulated_veh % 5) {
	0: spriteset_br_720_front_GA;
	1: spriteset_br_720_middle_panto_GA;
	4: spriteset_br_720_rear_GA;
	spriteset_br_720_middle_GA;
}

switch(FEAT_TRAINS, SELF, switch_br_720_spriteset_C2C_fwd, position_in_articulated_veh % 5) {
	0: spriteset_br_720_front_C2C;
	1: spriteset_br_720_middle_panto_C2C;
	4: spriteset_br_720_rear_C2C;
	spriteset_br_720_middle_C2C;
}

// Step 5 (reversed path): pick role from mirrored consist position.
// Using position_in_consist_from_end keeps car order visually fixed when the
// whole consist reverses, for any number of units.
switch(FEAT_TRAINS, SELF, switch_br_720_spriteset_GA_rev, position_in_consist_from_end % 5) {
	0: spriteset_br_720_front_GA_reversed;
	1: spriteset_br_720_middle_panto_GA_reversed;
	4: spriteset_br_720_rear_GA_reversed;
	spriteset_br_720_middle_GA_reversed;
}

switch(FEAT_TRAINS, SELF, switch_br_720_spriteset_C2C_rev, position_in_consist_from_end % 5) {
	0: spriteset_br_720_front_C2C_reversed;
	1: spriteset_br_720_middle_panto_C2C_reversed;
	4: spriteset_br_720_rear_C2C_reversed;
	spriteset_br_720_middle_C2C_reversed;
}

// Step 4 (reversed path): read mirrored vehicle cargo_subtype (var 0xF2).
// STORE_TEMP writes mirror offset; var[0x61,...] then reads that mirrored vehicle.
// This prevents incorrect livery swaps during reversal in multiple-working consists.
switch(FEAT_TRAINS, SELF, switch_br_720_rev_mirror_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]) {
	0: switch_br_720_spriteset_GA_rev;
	1: switch_br_720_spriteset_C2C_rev;
	switch_br_720_spriteset_GA_rev;
}

// Step 2: direction gate used by the top-level selector.
// Chooses forward stack or mirrored-reversed stack.
// If vcehicle is reversed, go to step 4, else go to step 3
switch(FEAT_TRAINS, PARENT, switch_br_720_direction_GA, vehicle_is_reversed) {
	1: switch_br_720_rev_mirror_livery;
	switch_br_720_spriteset_GA_fwd;
}

switch(FEAT_TRAINS, PARENT, switch_br_720_direction_C2C, vehicle_is_reversed) {
	1: switch_br_720_rev_mirror_livery;
	switch_br_720_spriteset_C2C_fwd;
}

// Step 1: top-level selector called by item_br_720 graphics.default.
switch(FEAT_TRAINS, SELF, switch_br_720_spriteset, cargo_subtype) {
	0: switch_br_720_direction_GA;
	1: switch_br_720_direction_C2C;
	switch_br_720_direction_GA;
}

// End of livery selection chain
//-------------------------------------------------------------------------------------------------------------------------



// Purchase menu subtype labels. (Livery options)
switch(FEAT_TRAINS, SELF, switch_br_720_subtype_text, cargo_subtype) {
	0: return string(str_GreaterAnglia);
	1: return string(str_C2C);
	return CB_RESULT_NO_TEXT;
}

// Define articulations
switch(FEAT_TRAINS, SELF, switch_br_720_articulated, extra_callback_info1) {
	// extra_callback_info1 is the articulated index being requested (1=2nd car, 2=3rd, ...).
	// Return this same item ID for cars 1..4 to build a fixed 5-car articulated unit.
	1..4: return item_br_720;
	return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

// Only the pantograph car (index 1 in each 5-car unit) shows electric effects.
switch(FEAT_TRAINS, SELF, switch_br_720_particles, position_in_articulated_veh % 5) {
	1: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER);
	visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
}

// Keep coupling length unrestricted for this unit.
switch(FEAT_TRAINS, SELF, switch_br_720_start_stop, num_vehs_in_consist) {	
	return CB_RESULT_NO_TEXT;
}

// Only allow Class 720 units to couple to Class 720 units.
switch(FEAT_TRAINS, SELF, switch_br_720_can_attach_wagon, vehicle_type_id) {
	item_br_720: return CB_RESULT_ATTACH_ALLOW;
	return string(STR_CANNOT_ATTACH);
}


// Class 720
item(FEAT_TRAINS, item_br_720, 448) {
	property {
		variant_group:					header_Aventra;
		name: string(STR_BR_720);
		climates_available: ALL_CLIMATES; // available in all climates
		introduction_date: date(2020, 03, 17);
		model_life: VEHICLE_NEVER_EXPIRES; // keep available forever		
		vehicle_life: 30; // years after vehicle is deemed "old" and should be replaced
		reliability_decay: 0; // dont reduce reliabilty, (will grow from 75% upwards over the years)
		refittable_cargo_classes: bitmask(CC_PASSENGERS);
		non_refittable_cargo_classes: bitmask();
		cost_factor: 95;
		loading_speed: 25;
		running_cost_factor: 55;
		cargo_allow_refit: [];
		cargo_disallow_refit: [];
		
		speed: 100 mph;
		power: 3100 hp;
		cargo_capacity: (685/5);
		weight: 120 ton;
		
		sprite_id: SPRITE_ID_NEW_TRAIN; // required
		refit_cost: 0;
		track_type: ELRL;
		ai_special_flag: AI_FLAG_PASSENGER; // tell ai that this is a passenger train
		running_cost_base: RUNNING_COST_ELECTRIC;
		misc_flags: TRAIN_FLAG_MU; // acts as multiple-unit
		dual_headed: 0; // set to 0 otherwise limited to 2 cars only
		engine_class: ENGINE_CLASS_ELECTRIC; // even if its 3rd rail, ELECTRIC would give overhead wire effects
		extra_power_per_wagon: 0 kW;
		tractive_effort_coefficient: 0.3;
		air_drag_coefficient: 0.06;
		length: 8; // full tile length vehicle
		visual_effect_and_powered: visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 2, DISABLE_WAGON_POWER); // visual fx
		extra_weight_per_wagon: 0 ton;
		bitmask_vehicle_info: 0;
	}
	
	graphics {
		default: switch_br_720_spriteset;
		purchase: spriteset_br_720_purchase;
		cargo_subtype_text: switch_br_720_subtype_text;
		articulated_part: switch_br_720_articulated;
		start_stop: switch_br_720_start_stop;	
		can_attach_wagon: switch_br_720_can_attach_wagon;
		visual_effect_and_powered: switch_br_720_particles;			
		additional_text: string(STR_DSC_BR_720);	
		cargo_capacity:						return(685/5 * param_pax);
		cost_factor:	return(GetAdjustedCost(95));
		running_cost_factor: return(GetAdjustedCost(55));
	}
}
